cmake_minimum_required(VERSION 3.0)
project(zabbix)

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")
include(cmake/utils.cmake)

set(zbx_home_dir "${CMAKE_CURRENT_SOURCE_DIR}/../..")
if(CMAKE_SIZEOF_VOID_P EQUAL 8)
    set(zbx_output_dir "${zbx_home_dir}/bin/win64")
else()
    set(zbx_output_dir "${zbx_home_dir}/bin/win32")
endif()
set(zbx_project_dir        "${zbx_home_dir}/src/zabbix_agent")
set(zbx_makefile_dir       "${zbx_home_dir}/build/win32/project")
set(zbx_config_include_dir "${zbx_home_dir}/build/win32/include")
set(zbx_include_dir        "${zbx_home_dir}/include")
set(zbx_src_dir            "${zbx_home_dir}/src")

# Get full list of build configurations
set(configuration_types ${CMAKE_CONFIGURATION_TYPES})
if(NOT configuration_types AND DEFINED CMAKE_BUILD_TYPE)
    list(APPEND configuration_types ${CMAKE_BUILD_TYPE})
endif()

# Common target properties (CMAKE_CXX_FLAGS)
zbx_change_default_compile_options("${CMAKE_CXX_FLAGS}" "" CMAKE_CXX_FLAGS)
foreach(configuration_type IN LISTS configuration_types)
    string(TOUPPER ${configuration_type} configuration_type_upper_case)
    set(configuration_options CMAKE_CXX_FLAGS_${configuration_type_upper_case})
    zbx_change_default_compile_options("${${configuration_options}}" "${configuration_type}" ${configuration_options})
endforeach(configuration_type)

# Common target properties (CMAKE_EXE_LINKER_FLAGS)
zbx_change_default_link_options("${CMAKE_EXE_LINKER_FLAGS}" CMAKE_EXE_LINKER_FLAGS)
foreach(configuration_type IN LISTS configuration_types)
    string(TOUPPER ${configuration_type} configuration_type_upper_case)
    set(configuration_options CMAKE_EXE_LINKER_FLAGS_${configuration_type_upper_case})
    zbx_change_default_link_options("${${configuration_options}}" ${configuration_options})
endforeach(configuration_type)

# Common target properties (CMAKE_RC_FLAGS)
zbx_change_default_rc_options("${CMAKE_RC_FLAGS}" CMAKE_RC_FLAGS)
foreach(configuration_type IN LISTS configuration_types)
    string(TOUPPER ${configuration_type} configuration_type_upper_case)
    set(configuration_options CMAKE_RC_FLAGS_${configuration_type_upper_case})
    zbx_change_default_rc_options("${${configuration_options}}" ${configuration_options})
endforeach(configuration_type)

find_package(MessageCompiler REQUIRED)
find_package(ManifestTool REQUIRED)

add_subdirectory(zabbix_agentd)
